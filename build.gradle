//
// Stanford Phrasal build specification for
// Gradle.
//
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'idea'

// Gradle java plugin
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Release version
version = '3.6.0'

// Gradle application plugin
mainClassName = "edu.stanford.nlp.mt.Phrasal"
applicationDefaultJvmArgs = ["-ea", "-server", "-XX:+UseParallelGC", "-XX:+UseParallelOldGC"]

// Jar creation
jar {
    manifest {
        attributes 'Implementation-Title': 'Stanford Phrasal',
                   'Implementation-Version': version,
		   'Main-Class': 'edu.stanford.nlp.mt.Phrasal'
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

compileJava {
    //enable compilation in a separate daemon process
    options.fork = true

    // File encoding
    options.encoding = 'UTF-8'

    //enable incremental compilation
    options.incremental = true
}

test {
    // Set to true to see stderr
    testLogging.showStandardStreams = false
}

// Configure build targets
sourceSets {
  main {
    java.srcDirs = ['src/']
    resources.srcDirs = ['resources/']
  }
  test {
    java.srcDirs = ['test/']
    resources.srcDirs = ['test-resources/','src-cc']
  }
  extra {
    java.srcDirs = ['src-extra/']
    resources.srcDirs = ['resources/']
  }
}

//
// KenLM tasks: compile and add to JVM library path
//
task compileKenLM(type: Exec) {
  commandLine 'src-cc/compile_JNI.sh'
}

//
// KenLM tools
//
task compileKenLMtools(type: Exec) {
  workingDir 'src-cc/kenlm'
  commandLine './bjam'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "src-cc"
}

//
// Dependencies and other build tasks
//
repositories {
    mavenCentral()
}

task listDeps {
    configurations.compile.each { File file -> println file.name }
}

task copyDeps(type: Copy) {
  from configurations.runtime
  into 'lib'
}

dependencies {
  // TERp jars that are no longer available on the web.
  compile fileTree(dir: 'lib', include: '*.jar')

  // lilt/core Maven dependencies
  compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.6.0'
  compile group: 'com.esotericsoftware', name: 'kryo', version: '3.0.1'
  compile group: 'it.unimi.dsi', name: 'fastutil', version: '7.0.12'
  compile group: 'com.google.guava', name: 'guava', version: '19.0'
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
  compile group: 'com.lmax', name: 'disruptor', version: '3.3.6'

  // Test dependencies
  testCompile group: 'junit', name: 'junit', version: '4.12'

  // Extra dependencies
  extraCompile sourceSets.main.output
  extraCompile sourceSets.test.output
  extraCompile configurations.testCompile
  extraRuntime configurations.testRuntime
  extraCompile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.2.1.v20140609'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.1.v20140609'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '9.2.1.v20140609'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.2.1.v20140609'
}

// Eclipse plugin setup
eclipse {
  classpath {
    defaultOutputDir = file('bin/')
    file {
      beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' }
      }
    }
  }
}

//
// Buildscript setup
//
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:1.4.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.owasp.dependencycheck'
