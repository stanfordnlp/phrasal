//
// Stanford Phrasal build specification for
// Gradle.
//
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven-publish'

// Gradle java plugin
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Release version
version = '3.6.0'

// Gradle application plugin
mainClassName = "edu.stanford.nlp.mt.Phrasal"
applicationDefaultJvmArgs = ["-ea", "-server", "-XX:+UseParallelGC", "-XX:+UseParallelOldGC"]

// Jar creation
jar {
    manifest {
        attributes 'Implementation-Title': 'Stanford Phrasal',
                   'Implementation-Version': version,
		   'Main-Class': 'edu.stanford.nlp.mt.Phrasal'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'edu.stanford.nlp'
            artifactId = 'Stanford Phrasal'
            version = '3.6.0'
        }
    }
}

compileJava {
    //enable compilation in a separate daemon process
    options.fork = true

    // File encoding
    options.encoding = 'UTF-8'
    
    //enable incremental compilation
    options.incremental = true
}

test {
    // Set to true to see stderr
    testLogging.showStandardStreams = false
}

// Configure build targets
sourceSets {
  main {
    java.srcDirs = ['src/']
    resources.srcDirs = ['resources/']
  }
  test {
    java.srcDirs = ['test/']
    resources.srcDirs = ['test-resources/','src-cc']
  }
  extra {
    java.srcDirs = ['src-extra/']
    resources.srcDirs = ['resources/']
  }
}

//
// KenLM tasks: compile and add to JVM library path
//
task compileKenLM(type: Exec) {
  commandLine 'src-cc/compile_JNI.sh'
}

//
// KenLM tools
//
task compileKenLMtools(type: Exec) {
  workingDir 'src-cc/kenlm'
  commandLine './bjam'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "src-cc"
}

//
// Dependencies and other build tasks
//
repositories {
    mavenCentral()
}

task listDeps {
  doLast {
    configurations.implementation.api.each { File file -> println file.name }
  }
}

task copyDeps(type: Copy) {
  from configurations.runtimeClasspath
  into 'lib'
}

dependencies {
  // TERp jars that are no longer available on the web.
  implementation fileTree(dir: 'lib', include: '*.jar')
  
  // lilt/core Maven dependencies
  implementation group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.6.0'
  implementation group: 'com.esotericsoftware', name: 'kryo', version: '3.0.1'
  implementation group: 'it.unimi.dsi', name: 'fastutil', version: '7.0.12'
  implementation group: 'com.google.guava', name: 'guava', version: '19.0'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
  implementation group: 'com.lmax', name: 'disruptor', version: '3.3.6'
  
  // Test dependencies
  testImplementation group: 'junit', name: 'junit', version: '4.12'

  // Extra dependencies
  extraImplementation sourceSets.main.output
  extraImplementation sourceSets.test.output
  extraImplementation configurations.testImplementation
  extraRuntimeOnly configurations.testRuntimeOnly
  extraImplementation group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
  extraImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.2.1.v20140609'
  extraImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.1.v20140609'
  extraImplementation group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '9.2.1.v20140609'
  extraImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.2.1.v20140609'
}

// Eclipse plugin setup
eclipse {
  classpath {
    defaultOutputDir = file('bin/')
    file {
      beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' }
      }
    }
  }
}

//
// Buildscript setup
//
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:1.4.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.owasp.dependencycheck'

