//
// Stanford Phrasal build specification for
// Gradle.
//
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'

// Gradle java plugin
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Release version
version = '3.7.1-SNAPSHOT'

repositories {
    maven {
        url "${nexusUrl}/repository/maven-public"
        credentials {
            username "${nexusUsername}"
            password "${nexusPassword}"
        }
        authentication {
            basic(BasicAuthentication)
        }
    }
}

// Gradle application plugin
mainClassName = "edu.stanford.nlp.mt.Phrasal"
applicationDefaultJvmArgs = ["-ea", "-server", "-XX:+UseParallelGC", "-XX:+UseParallelOldGC"]

// Jar creation
jar {
    manifest {
        attributes 'Implementation-Title': 'Stanford Phrasal',
                   'Implementation-Version': version,
		   'Main-Class': 'edu.stanford.nlp.mt.Phrasal'
    }
}

configurations.archives.artifacts.removeAll {
    // exclude from the archives configuration all artifacts that were generated by distZip & distTar
    def depTasks = it.getBuildDependencies().getDependencies()
    depTasks.contains(distZip) || depTasks.contains(distTar)
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${nexusUrl}/repository/maven-releases") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            pom.groupId = 'com.lilt'
            pom.artifactId = 'phrasal'
            pom.version = version
       }
    }
}

compileJava {
    //enable compilation in a separate daemon process
    options.fork = true

    // File encoding
    options.encoding = 'UTF-8'
    
    //enable incremental compilation
    options.incremental = true
}

test {
    // Set to true to see stderr
    testLogging.showStandardStreams = false
}

// Configure build targets
sourceSets {
  main {
    java.srcDirs = ['src/']
    resources.srcDirs = ['resources/']
  }
  test {
    java.srcDirs = ['test/']
    resources.srcDirs = ['test-resources/','src-cc']
  }
  extra {
    java.srcDirs = ['src-extra/']
    resources.srcDirs = ['resources/']
  }
}

//
// KenLM tasks: compile and add to JVM library path
//
task compileKenLM(type: Exec) {
  commandLine 'src-cc/compile_JNI.sh'
}

//
// KenLM tools
//
task compileKenLMtools(type: Exec) {
  workingDir 'src-cc/kenlm'
  commandLine './bjam'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "src-cc"
}

//
// Dependencies and other build tasks
//
repositories {
    mavenCentral()
}

task listDeps {
  doLast {
    configurations.compile.each { File file -> println file.name }
  }
}

task copyDeps(type: Copy) {
  from configurations.runtime
  into 'lib'
}

configurations {
  // Prevent corenlp from pulling in an ancient version of xerces
  all*.exclude group: 'xerces', module: 'xercesImpl'
}

dependencies {
  // TERp jars that are no longer available on the web.
  compile fileTree(dir: 'lib', include: '*.jar')
  
  // lilt/core Maven dependencies
  compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.6.0'
  compile group: 'com.esotericsoftware', name: 'kryo', version: '5.1.1'
  compile group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.4'
  compile group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  compile group: 'com.lmax', name: 'disruptor', version: '3.4.4'
  
  // Test dependencies
  testCompile group: 'junit', name: 'junit', version: '4.13.2'

  // Extra dependencies
  extraCompile sourceSets.main.output
  extraCompile sourceSets.test.output
  extraCompile configurations.testCompile
  extraRuntime configurations.testRuntime
  extraCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.4.40.v20210413'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.40.v20210413'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '9.4.40.v20210413'
  extraCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.40.v20210413'
}

// Eclipse plugin setup
eclipse {
  classpath {
    defaultOutputDir = file('bin/')
    file {
      beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' }
      }
    }
  }
}

//
// Buildscript setup
//
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:1.4.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.owasp.dependencycheck'

